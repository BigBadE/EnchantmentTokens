/*
 * Custom enchantments for Minecraft
 * Copyright (C) 2021 Big_Bad_E
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

configurations {
    resolveImplementation
    resolveImplementation.extendsFrom(implementation)
}

sourceSets {
    test {
        compileClasspath += configurations.resolveImplementation
        compileClasspath += configurations.shadow
        runtimeClasspath += configurations.resolveImplementation
        runtimeClasspath += configurations.shadow
    }
}

dependencies {
    implementation project(':api')
    //fileTree doesn't work as a transitive dependency for some reason.
    implementation fileTree(dir: '../api/lib', include: '*.jar')

    implementation('me.lucko:commodore:1.+') {
        exclude module: 'brigadier'
    }

    implementation 'org.bstats:bstats-bukkit:2.+'

    shadow 'org.spigotmc:spigot-api:1.15.+'
    shadow 'com.comphenix.protocol:ProtocolLib:4.+'
    shadow 'net.milkbowl.vault:VaultAPI:1.+'
    shadow 'com.github.SkriptLang:Skript:2.+'
    shadow 'com.google.code.findbugs:jsr305:3.+'
    shadow 'com.mojang:brigadier:1.+'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.+'

    testImplementation 'com.github.seeseemelk:MockBukkit:v1.+'

    shadow 'org.projectlombok:lombok:1.+'
    annotationProcessor 'org.projectlombok:lombok:1.+'

    testCompileOnly 'org.projectlombok:lombok:1.+'
    testAnnotationProcessor 'org.projectlombok:lombok:1.+'
}

shadowJar {
    archiveClassifier.set('')
    archiveBaseName.set('EnchantmentTokens')
    relocate 'me.lucko', 'com.bigbade'
    relocate 'org.bstats', 'com.bigbade'
    relocate 'co.aikar', 'com.bigbade'

    exclude 'META-INF/**'

    from('../') {
        include 'localization/*.properties'
        include 'commodore/*.commodore'
    }

    from('../resources') {
        include '**'
    }
}
